bool LOGGING = true;
#define LOGFILE "log.txt"
#define LOGFILE_SIZE 10000
#include "logging.nxc"

//INCOMING COMMANDS FROM MINDSQUALLS
#define INCOMING_MOVE_TO_POS 50
#define INCOMING_GET_SENSOR_MEASUREMENT 51
#define INCOMING_GET_POS 52

//OUTGOING COMMANDS TO BE RECEIVED BY MINDSQUALLS
#define OUTGOING_WHERE_AM_I 0
#define OUTGOING_I_AM_THERE 1
#define OUTGOING_SEND_SENSOR_DATA 2

//MAILBOXES TO BE USED FOR INCOMING AND OUTGOING COMMANDS
#define INBOX           1
#define OUTBOX          0

#include "Structs.nxc"
#include "Communication.nxc"
#include "Navigation.nxc"
#include "Sensor.nxc"

task main(){
     openLogFile(LOGFILE, LOGFILE_SIZE);
     writeLogString("START");
     writeLogString("---");

     // Don't forget this!
     InitSpeedFromPosLUT();

     // Setup for later use
     string in = "";
     string cmd = "";
     string args = "";
     byte cmdType;
     bool clockwise = true;

     // clear mailboxes
     in = "...";
     while(StrLen(in) > 0) {
         ReceiveRemoteString(INBOX, true, in);
     }
     in = "...";
     while(StrLen(in) > 0) {
         ReceiveRemoteString(OUTBOX, true, in);
     }

     in = "";
     
     while(true){
          //Check inbox for new commands
          ReceiveRemoteString(INBOX, true, in);

          //If there is a command, convert to int and do switch
          //  else reset loop
          if (StrLen(in) > 0){
             cmd = SubStr(in, 0, 2);
             cmdType = StrToNum(cmd);
             args = SubStr(in, 1, StrLen(in)-1);
             
             writeLogString("IN: " + in);
             
             switch(cmdType){
                 case INCOMING_MOVE_TO_POS:             
                 string x = SubStr(in, 2, 7);
                 string y = SubStr(in, 9, 7);

                 Point p;
                 p.x = StrToNum(x);
                 p.y = StrToNum(y);

                 ClearScreen();
                 TextOut(0, LCD_LINE1, "Going to:");
                 TextOut(2, LCD_LINE2, "x:" + NumToStr(p.x));
                 TextOut(2, LCD_LINE3, "y:" + NumToStr(p.y));

                 writeLogString("GOING TO:");
                 writeLogString("  x: " + NumToStr(p.x));
                 writeLogString("  y: " + NumToStr(p.y));

                 GotoPoint(p);
                 IAmThere();
                 break;
                 
                 case INCOMING_GET_SENSOR_MEASUREMENT :
                 AdjustAngle();
                 string s = "";
                 s = NumToStr(OUTGOING_SEND_SENSOR_DATA);
                 ClearScreen();
                 TextOut(0, LCD_LINE1, "Measuring...");
                 s = StrCat(s,rotate_and_measure(clockwise));
                 TextOut(1, LCD_LINE1, "Sending...");
                 SendMessage(OUTBOX,s);
                 
                 if(clockwise)
                 {
                  clockwise = false;
                 }
                 else
                 {
                  clockwise = true;
                 }
                 
                 break;

             }//end switch cmdtype
             
             in = "";
             args = "";
          }//end if strlen in
          
          if (ButtonPressed(BTNCENTER, false)) {
            // release all motors
            MotorOff(OUT_A);
            MotorOff(OUT_B);
            MotorOff(OUT_C);
            PlayTone(440, 500);
            Wait(100);

            // release all motors again
            MotorOff(OUT_A);
            MotorOff(OUT_B);
            MotorOff(OUT_C);

            TextOut(5,LCD_LINE7, "EMERGENCY STOP    ", false);
            TextOut(2,LCD_LINE8, "Restart program!  ", false);
            Wait(2500);
            StopAllTasks();
          }//end if buttenpressed
     }//end while true
     
     closeLogFile();
}//end task main
